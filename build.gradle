plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
	id "org.hibernate.orm" version '7.2.0.Final' apply false
	id 'org.graalvm.buildtools.native' version '0.11.3' apply false
	id 'jacoco'
	id "org.sonarqube" version "7.2.2.6593"
}

sonar {
	properties {
		property "sonar.projectKey", "ister-app_server"
		property "sonar.organization", "ister-app"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.hibernate.orm'
	apply plugin: 'jacoco'

	group = 'app.ister'
	version = '0.0.1-SNAPSHOT'

	if (project.name == 'server') {
		apply plugin: 'org.springframework.boot'
		apply plugin: 'org.graalvm.buildtools.native'
	}

	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
		}
	}

	repositories {
		mavenCentral()
		maven {
			url = uri("https://maven.pkg.github.com/ister-app/blurhash-java")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
			}
		}
	}

	configurations {
		mockitoAgent
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		testImplementation("org.mockito:mockito-core")
		mockitoAgent("org.mockito:mockito-core") { transitive = false }
	}

	tasks.named('test') {
		jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
		useJUnitPlatform()
		// When enable native image trace agent, disable jacoco.
//		jvmArgs '-agentlib:native-image-agent=config-merge-dir=src/main/resources/META-INF/native-image'
	}

	tasks.withType(JavaCompile) {
		options.compilerArgs += ['-parameters']
	}

	hibernate {
		enhancement {
			enableAssociationManagement = true
		}
	}

	jacocoTestReport {
		reports {
			xml.required = true
		}
	}
}
